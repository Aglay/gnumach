dnl Configure fragment for x86_64.

dnl Copyright (C) 1999, 2004, 2006, 2007, 2008 Free Software Foundation, Inc.

dnl Permission to use, copy, modify and distribute this software and its
dnl documentation is hereby granted, provided that both the copyright
dnl notice and this permission notice appear in all copies of the
dnl software, derivative works or modified versions, and any portions
dnl thereof, and that both notices appear in supporting documentation.
dnl
dnl THE FREE SOFTWARE FOUNDATION ALLOWS FREE USE OF THIS SOFTWARE IN ITS
dnl "AS IS" CONDITION.  THE FREE SOFTWARE FOUNDATION DISCLAIMS ANY
dnl LIABILITY OF ANY KIND FOR ANY DAMAGES WHATSOEVER RESULTING FROM THE
dnl USE OF THIS SOFTWARE.

#
# Definitions.
#

[case $host_cpu in
  x86_64)]
    AM_CONDITIONAL([HOST_x86_64], [true])

    # Some of the x86_64-specific code checks for these.
    AC_DEFINE([__ELF__], [1], [__ELF__])
    AC_DEFINE([x86_46], [1], [x86_64])

    # Determines the size of the CPU cache line.
    AC_DEFINE([CPU_L1_SHIFT], [6], [CPU_L1_SHIFT])

    [# Does the architecture provide machine-specific interfaces?
    mach_machine_routines=1;;
  *)]
    AM_CONDITIONAL([HOST_x86_64], [false])[;;
esac

case $host_platform in
  at)]
    AM_CONDITIONAL([PLATFORM_at], [true])[;;
  *)]
    AM_CONDITIONAL([PLATFORM_at], [false])[;;
esac

#
# Formerly in `x86_64/bogus/'.
#

ncom=0
nlpr=0
case $host_platform:$host_cpu in
  at:x86_64)
    ncom=4
    nlpr=1

    # x86_64/bogus/platforms.h]
    AC_DEFINE([AT386], [1], [AT386])[;;
  xen:x86_64)
    # TODO.  That should probably not be needed.
    ncom=1
    # TODO.  That should probably not be needed.
    # x86_64/bogus/platforms.h]
    AC_DEFINE([AT386], [1], [AT386])[;;
  *)
    :;;
esac]

# x86_64/bogus/com.h
AC_DEFINE_UNQUOTED([NCOM], [$ncom], [NCOM])

# x86_64/bogus/lpr.h
AC_DEFINE_UNQUOTED([NLPR], [$nlpr], [NLPR])

#
# Options.
#

AC_ARG_ENABLE([lpr],
  AS_HELP_STRING([--enable-lpr], [lpr device; on x86_64-at enabled by default]))
[case $host_platform:$host_cpu in
  at:x86_64)
    case $enable_device_drivers in
      default)
        enable_lpr=${enable_lpr-yes};;
      *)
        enable_lpr=${enable_lpr-no};;
    esac;;
  *)
    if [ x"$enable_lpr" = xyes ]; then]
      AC_MSG_ERROR([cannot enable `lpr' in this configuration.])
    [fi;;
esac
if [ x"$enable_lpr" = xyes ]; then]
  AC_DEFINE([MACH_LPR], [], [lpr device])
  AM_CONDITIONAL([enable_lpr], [true])
[else]
  AM_CONDITIONAL([enable_lpr], [false])
[fi]


AC_DEFINE([PAE], [1], [PAE support])
AM_CONDITIONAL([enable_pae], [true])

dnl Local Variables:
dnl mode: autoconf
dnl End:
